/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.5.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'maven-publish'

version = '0.1.0-SNAPSHOT'
sourceCompatibility = 1.8

def mvnGroupId = 'net.tsamaya'

def mvnReleasesURL = "s3://my-artifacts.s3.eu-west-2.amazonaws.com/m2/releases"
def mvnSnapshotsURL = "s3://my-artifacts.s3.eu-west-2.amazonaws.com/m2/snapshots"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    maven {
        url mvnReleasesURL
        credentials(AwsCredentials) {
            accessKey AWS_ACCESS_KEY
            secretKey AWS_SECRET_KEY
        }
    }

    maven {
        url mvnSnapshotsURL
        credentials(AwsCredentials) {
            accessKey AWS_ACCESS_KEY
            secretKey AWS_SECRET_KEY
        }
    }

    mavenLocal()
    jcenter()
}

publishing {
    repositories {
        maven {
            if(project.version.endsWith('-SNAPSHOT')) {
              url mvnSnapshotsURL
            } else {
              url mvnReleasesURL
            }
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId mvnGroupId
            artifactId rootProject.name
            version version

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:21.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}
